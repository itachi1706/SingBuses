apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

def isGHActions = System.getenv("GITHUB_ACTIONS") ?: false
def appNamespace = "com.itachi1706.busarrivalsg"

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    namespace appNamespace

    defaultConfig {
        applicationId appNamespace
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 789
        versionName "5.0.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            firebaseCrashlytics { mappingFileUploadEnabled false } // Disabled mapping file uploading for DEBUG builds
            multiDexEnabled true
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    installation {
        installOptions '-d', '-t'
        timeOutInMs 10 * 60 * 1000
    }
    lint {
        abortOnError !isGHActions
    }
}

configurations {
    all {
        exclude module: 'httpclient'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation platform('com.google.firebase:firebase-bom:29.2.0')
    implementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.itachi1706.appupdater:appupdater:2.4.3'
    implementation 'com.itachi1706.helpers:helperlib:1.3.7'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.android.gms:play-services-auth:20.1.0'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'me.jfenn:Attribouter:0.1.9'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.itachi1706.cepaslib:cepaslib:2.3.0'
}

apply plugin: 'com.google.gms.google-services'